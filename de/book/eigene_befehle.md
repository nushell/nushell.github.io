# Eigene Befehle

Die FÃ¤higkeit von Nu, lange Pipelines zu verarbeiten, erlauben es groÃŸe Kontrolle Ã¼ber Daten und das System zu haben. Das Ganze kommt allerdings zum Preis von viel Tipparbeit. Idealerweise sollte es eine MÃ¶glichkeit geben, mÃ¼hsam gebaute Pipelines zu speichern und wieder und wieder auszufÃ¼hren.

Hier kommen eigene Befehle ins Spiel.

Eine beispielhafte Definition eines eigenen Befehls sieht wie folgt aus:

```nushell
def greet [name] {
  echo "hello" $name
}
```

In dieser Definition, wird ein Befehl `greet` beschrieben, der einen Parameter `name` konsumiert. Nach diesem Parameter erfolgt die Beschreibung was passiert, wenn der Befehl ausgefÃ¼hrt wird. Wenn der Befehl aufgerufen wird, wird der Wert, der als Parameter `name` Ã¼bergeben wurde, in die Variable `$name` geschrieben, die im Codeblock verfÃ¼gbar ist.

Um den obigen Befehl auszufÃ¼hren wird er wie ein eingebauter Befehl aufgerufen:

```
> greet "world"
```

Wenn das getan wird, wird eine Ausgabe erzeugt, die wie die der eingebauten Befehle aussieht:

```
â”€â”€â”€â”¬â”€â”€â”€â”€â”€â”€â”€
 0 â”‚ hello 
 1 â”‚ world 
â”€â”€â”€â”´â”€â”€â”€â”€â”€â”€â”€
```

## Namen von Befehlen

In Nushell ist ein valider Name eines Befehls ein String aus Zeichen oder ein String in AnfÃ¼hrungszeichen. Beispiele hierfÃ¼r sind: `greet`, `get-size`, `mycommand123`, `"mycommand"`, `ðŸ˜Š` und `123`. 

_Hinweis: Es wird empfohlen Worte in Befehlen mit `-` zur besseren Lesbarkeit zu trennen._ Beispiele: `get-size` anstatt `getsize` oder `get_size`.

## Unterbefehle

Es ist auch mÃ¶glich Unterbefehle zu definieren. Dazu wird der Unterbefehl vom Superbefehl durch ein Leerzeichen getrennt. Wenn beispielsweise der Befehl `str` durch einen Unterbefehl `mycommand` erweitert werden soll, funktioniert das wie folgt:

```nushell
def "str mycommand" [] {
  echo hello
}
```

Jetzt kann der eigene Unterbefehl aufgerufen werden, als ob er ein eingebauter Befehl von `str` wÃ¤re:

```
> str mycommand
```

## Typen von Parametern

Wenn eigene Befehle definiert werden, kann optional auch der Typ jedes Parameters angegeben werden. Das obige Beispiel kann beispielsweise wie folgt abgeÃ¤ndert werden:

```nushell
def greet [name: string] {
  echo "hello" $name
}
```

Die Typen der Parameter anzugeben ist optional. Nushell erlaubt es diese wegzulassen und behandelt diese dann als Typ `any`. Es kann also jede Art von Typ verarbeitet werden. Wenn ein Typ angegeben wurde, Ã¼berprÃ¼ft Nushell den Typ, wenn die Funktion aufgerufen wird.

Beispielhaft soll nur noch ein `int` als Typ erlaubt sein:

```nushell
def greet [name: int] {
  echo "hello" $name
}

greet world
```

Wenn versucht wird, den oberen Code auszufÃ¼hren, wird Nu darauf aufmerksam machen, dass die Typen nicht passen und die AusfÃ¼hrung stoppen:

```
error: Type Error
  â”Œâ”€ shell:6:7
  â”‚
5 â”‚ greet world
  â”‚       ^^^^^ Expected int, found world
```

Dies kann dabei helfen Nutzer darauf aufmerksam zu machen, welche Art von Typ erlaubt ist.

Die aktuell erlaubten Typen sind (mit Version 0.59.0 und neuer):

 - `any`
 - `block`
 - `cell-path`
 - `duration`
 - `path`
 - `expr`
 - `filesize`
 - `glob`
 - `int`
 - `math`
 - `number`
 - `operator`
 - `range`
 - `cond`
 - `bool`
 - `signature`
 - `string`
 - `variable`

## Flags

ZusÃ¤tzlich zu den obigen Parametern, kÃ¶nnen auch namenabhÃ¤ngige Parameter verwendet werden, indem Flags fÃ¼r eigene Befehle definiert werden.

Zum Beispiel:

```nushell
def greet [
  name: string
  --age: int
] {
  echo $name $age
}
```

In der obigen Definition von `greet`, werden ein fester Parameter `name` und eine Flag `age` definiert. Damit ist es mÃ¶glich, dem Befehl `greet` optional den Parameter `age` zu Ã¼bergeben.

Das obige Beispiel kann wie folgt aufgerufen werden:

```
> greet world --age 10
```

Oder:

```
> greet --age 10 world
```

Oder gleich ganz ohne Flag:

```
> greet world
```

Flags kÃ¶nnen auch so definiert werden, dass es eine Kurzform gibt. Das erlaubt es sowohl eine kurze als auch eine einfach lesbare lange Flag fÃ¼r die selbe Aufgabe zu haben.

Das Beispiel wird hier, um eine Kurzform fÃ¼r die Flag `age` erweitert:

```nushell
def greet [
  name: string
  --age (-a): int
] {
  echo $name $age
}
```

*Hinweis:* Flags sind benannt nach der langen Form des Namens. Im obigen Beispiel erfolgt der Zugriff immer Ã¼ber `$age` und nicht Ã¼ber `$a`.

Nun kann diese neue Version von `greet` wie folgt aufgerufen werden:

```
> greet -a 10 hello
```


## Dokumentation fÃ¼r den eigenen Befehl

Um Nutzern eines eigenen Befehls zu helfen, kÃ¶nnen diese und ihre Parameter mit zusÃ¤tzlichen Beschreibungen versehen werden.

Es wird weiterhin das obige Beispiel verwendet:

```nushell
def greet [
  name: string
  --age (-a): int
] {
  echo $name $age
}
```

Wenn der Befehl definiert ist kann `help greet` aufgerufen werden, um Informationen zum Befehl zu erhalten:

```
Usage:
  > greet <name> {flags} 

Parameters:
  <name> 

Flags:
  -h, --help: Display this help message
  -a, --age <integer>  
```

Wie zu sehen ist, werden der Parameter und die Flag, die definiert wurden, aufgelistet. ZusÃ¤tzlich gibt es noch die Flag `-h`, die jeder Befehl hat.

Um diese Hilfe zu verbessern, kÃ¶nnen Beschreibungen zur Definition hinzugefÃ¼gt werden:

```nushell
# A greeting command that can greet the caller
def greet [
  name: string      # The name of the person to greet
  --age (-a): int   # The age of the person
] {
  echo $name $age
}
```

Diese Kommentare, die zur Definition und den Parametern hinzugefÃ¼gt wurden, werden sichtbar, wenn die Hilfe zum Befehl aufgerufen wird.

Wenn jetzt `help greet` ausgefÃ¼hrt wird, wird ein hilfreicherer Text angezeigt:

```
A greeting command that can greet the caller

Usage:
  > greet <name> {flags} 

Parameters:
  <name> The name of the person to greet

Flags:
  -h, --help: Display this help message
  -a, --age <integer>: The age of the person
```

## Ausgabe

Eigene Befehle streamen ihre Ausgabe gleich wie eingebaute Befehle. Beispielsweise soll die folgende Pipeline umgebaut werden:

```nushell
> ls | get name
```

`ls` soll jetzt in einen neuen, eigenen Befehl verschoben werden:

```nushell
def my-ls [] { ls }
```

Die Ausgabe dieses Befehls, kann identisch zur Ausgabe von `ls` verwendet werden.

```
> my-ls | get name
â”€â”€â”€â”¬â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
 0 â”‚ myscript.nu           
 1 â”‚ myscript2.nu          
 2 â”‚ welcome_to_nushell.md 
â”€â”€â”€â”´â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
```

Das erlaubt es sehr einfach eigene Befehle zu definieren und deren Ausgabe zu verwenden. Ein Hinweis: Es werden keine return Statements wie in anderen Sprachen verwendet. Stattdessen werden in Nushell Pipelines gebaut, die ihre Ausgabe zur verbundenen Pipeline streamen.

## Eingabe 

Eigene Befehle kÃ¶nnen, wie andere Befehle, auch Eingaben verarbeiten. Diese Eingabe wird durch die Pipeline an den Codeblock des eigenen Befehls Ã¼bergeben.

Hier soll nun beispielhaft ein eigener echo-Befehl definiert werden, der eine weitere Zeile nach jeder Zeile der Eingabe ausgibt:

```nushell
def my-echo [] {
  each {
    echo $it "--"
  }
}
```

Wenn dieser neue Befehl nun in einer Pipeline aufgerufen wird, sieht die Ausgabe wie folgt aus:

```
> echo foo bar | my-echo
â”€â”€â”€â”¬â”€â”€â”€â”€â”€
 0 â”‚ foo 
 1 â”‚ --  
 2 â”‚ bar 
 3 â”‚ --  
â”€â”€â”€â”´â”€â”€â”€â”€â”€
```

## Persistenz

Um Informationen darÃ¼ber zu erhalten, wie eigene Befehle bei jedem Start von Nushell verfÃ¼gbar bleiben, sei auf das [Konfigurationskapitel](configuration.md#startup-befehle) verwiesen.
